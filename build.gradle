// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.3.61'
    ext.versions = [
            'java'               : JavaVersion.VERSION_1_8,
            'androidGradlePlugin': '3.6.3',
            'googleServices'     : '3.2.1',
            'compileSdk'         : 29,
            'buildTools'         : '28.0.3',
            'minSdk'             : 16,
            'targetSdk'          : 29,
            'supportLibrary'     : '26.1.0',
            'constraintLayout'   : '1.1.3',
            'firebase'           : '10.0.1',
            'retrofit'           : '2.0.0-beta4',
            'okhttp'             : '3.6.0',
            'ion'                : '2.1.8',
            'videoAndroid'       : '5.6.0'
    ]

    ext.getSecretProperty = { key, defaultValue ->
        def value = System.getenv(key)

        if (value == null) {
            Properties properties = new Properties()
            if (project.rootProject.file('local.properties').exists()) {
                properties.load(project.rootProject.file('local.properties').newDataInputStream())
                value = properties.getProperty(key)
            }
        }

        if (value == null) {
            value = defaultValue
        }

        return value
    }
    repositories {
        google()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:${versions.androidGradlePlugin}"
        classpath "com.google.gms:google-services:${versions.googleServices}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        mavenCentral()
    }
}
